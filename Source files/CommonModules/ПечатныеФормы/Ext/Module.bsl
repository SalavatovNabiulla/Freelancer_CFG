Функция ПолучитьПлатежнуюИнформацию()
	СчетПоУмолчанию = Справочники.ДенежныеСчета.НайтиПоРеквизиту("СчетПоУмолчанию",Истина);
	ПлатежнаяИнформация = Новый Соответствие;
	//
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ФинансоваяИнформацияДенежныхСчетовСрезПоследних.ВидИнформации КАК ВидИнформации,
	               |	ФинансоваяИнформацияДенежныхСчетовСрезПоследних.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.ФинансоваяИнформацияДенежныхСчетов.СрезПоследних КАК ФинансоваяИнформацияДенежныхСчетовСрезПоследних
	               |ГДЕ
	               |	ФинансоваяИнформацияДенежныхСчетовСрезПоследних.ДенежныйСчёт = &ДенежныйСчёт";
	Запрос.УстановитьПараметр("ДенежныйСчёт",СчетПоУмолчанию);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		ПлатежнаяИнформация.Вставить(ВыборкаЗапроса.ВидИнформации,ВыборкаЗапроса.Значение);		
	КонецЦикла;
	Возврат ПлатежнаяИнформация;
КонецФункции

Функция ПолучитьДанныеОВладельце()
	ВидыИнформацииОВладельце = Новый Массив;
	ВидыИнформацииОВладельце.Добавить(Справочники.ВидыИнформацииОВладельце.Фамилия);
	ВидыИнформацииОВладельце.Добавить(Справочники.ВидыИнформацииОВладельце.Имя);
	ВидыИнформацииОВладельце.Добавить(Справочники.ВидыИнформацииОВладельце.Отчество);
	ВидыИнформацииОВладельце.Добавить(Справочники.ВидыИнформацииОВладельце.ИНН);
	ВидыИнформацииОВладельце.Добавить(Справочники.ВидыИнформацииОВладельце.НомерТелефона);
	ВидыИнформацииОВладельце.Добавить(Справочники.ВидыИнформацииОВладельце.ЭлектроннаяПочта);
	//
	ДанныеОВЛадельце = Новый Структура;
	ДанныеОВЛадельце.Вставить("Фамилия","");
	ДанныеОВЛадельце.Вставить("Имя","");
	ДанныеОВЛадельце.Вставить("Отчество","");
	ДанныеОВЛадельце.Вставить("ИНН","");
	ДанныеОВЛадельце.Вставить("НомерТелефона","");
	ДанныеОВЛадельце.Вставить("ЭлектроннаяПочта","");
	//
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ДанныеОВладельцеСрезПоследних.ВидИнформацииОВладельце КАК ВидИнформацииОВладельце,
	               |	ДанныеОВладельцеСрезПоследних.Значение КАК Значение
	               |ИЗ
	               |	РегистрСведений.ДанныеОВладельце.СрезПоследних КАК ДанныеОВладельцеСрезПоследних
	               |ГДЕ
	               |	ДанныеОВладельцеСрезПоследних.ВидИнформацииОВладельце В(&ВидИнформацииОВладельце)";
	Запрос.УстановитьПараметр("ВидИнформацииОВладельце",ВидыИнформацииОВладельце);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		Если ВыборкаЗапроса.ВидИнформацииОВладельце = Справочники.ВидыИнформацииОВладельце.Фамилия Тогда
			ДанныеОВЛадельце.Фамилия = ВыборкаЗапроса.Значение;
		ИначеЕсли ВыборкаЗапроса.ВидИнформацииОВладельце = Справочники.ВидыИнформацииОВладельце.Имя Тогда
			ДанныеОВЛадельце.Имя = ВыборкаЗапроса.Значение;
		ИначеЕсли ВыборкаЗапроса.ВидИнформацииОВладельце = Справочники.ВидыИнформацииОВладельце.Отчество Тогда
			ДанныеОВЛадельце.Отчество = ВыборкаЗапроса.Значение;
		ИначеЕсли ВыборкаЗапроса.ВидИнформацииОВладельце = Справочники.ВидыИнформацииОВладельце.ИНН Тогда
			ДанныеОВЛадельце.ИНН = ВыборкаЗапроса.Значение;
		ИначеЕсли ВыборкаЗапроса.ВидИнформацииОВладельце = Справочники.ВидыИнформацииОВладельце.НомерТелефона Тогда
			ДанныеОВЛадельце.НомерТелефона = ВыборкаЗапроса.Значение;
		ИначеЕсли ВыборкаЗапроса.ВидИнформацииОВладельце = Справочники.ВидыИнформацииОВладельце.ЭлектроннаяПочта Тогда
			ДанныеОВЛадельце.ЭлектроннаяПочта = ВыборкаЗапроса.Значение;
		КонецЕсли;
	КонецЦикла;
	//
	Возврат ДанныеОВЛадельце;
КонецФункции

Функция ПолучитьДанныеОКонтрагенте(Контрагент)
	ДанныеОКонтрагенте = Новый Структура;
	ДанныеОКонтрагенте.Вставить("Наименование",Контрагент.Наименование);
	ДанныеОКонтрагенте.Вставить("НомерТелефона","");
	ДанныеОКонтрагенте.Вставить("ЭлектроннаяПочта","");
	//
	ВидыКонтактнойИнформации = Новый Массив;
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.НомерТелефона);
	ВидыКонтактнойИнформации.Добавить(Справочники.ВидыКонтактнойИнформации.ЭлектроннаяПочта);
	//
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	КонтактныеДанныеКонтрагентовСрезПоследних.Значение КАК Значение,
	               |	КонтактныеДанныеКонтрагентовСрезПоследних.ВидКонтактнойИнформации КАК ВидКонтактнойИнформации
	               |ИЗ
	               |	РегистрСведений.КонтактныеДанныеКонтрагентов.СрезПоследних КАК КонтактныеДанныеКонтрагентовСрезПоследних
	               |ГДЕ
	               |	КонтактныеДанныеКонтрагентовСрезПоследних.Контрагент = &Контрагент
	               |	И КонтактныеДанныеКонтрагентовСрезПоследних.ВидКонтактнойИнформации В(&ВидКонтактнойИнформации)";
	Запрос.УстановитьПараметр("Контрагент",Контрагент);
	Запрос.УстановитьПараметр("ВидКонтактнойИнформации",ВидыКонтактнойИнформации);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		Если ВыборкаЗапроса.ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.НомерТелефона Тогда
			ДанныеОКонтрагенте.НомерТелефона = ВыборкаЗапроса.Значение;
		ИначеЕсли ВыборкаЗапроса.ВидКонтактнойИнформации = Справочники.ВидыКонтактнойИнформации.НомерТелефона Тогда
			ДанныеОКонтрагенте.ЭлектроннаяПочта = ВыборкаЗапроса.Значение;
		КонецЕсли;
	КонецЦикла;
	//
	Возврат ДанныеОКонтрагенте;
КонецФункции

Функция ПолучитьНазвание(СсылкаНаДокумент)
	ОбъектДок = СсылкаНаДокумент.ПолучитьОбъект();
	Название = "Акт выполненных работ №"+Строка(ОбъектДок.Номер)+" от "+Строка(Формат(ОбъектДок.Дата,"ДФ=dd.MM.yyyy"))+".xls";
	Возврат Название;
КонецФункции

Функция ЗаполнитьАктВыполненныхРабот(ТабДок,СсылкаНаДокумент) Экспорт
	Акт = Новый Структура;
	Акт.Вставить("Название",ПолучитьНазвание(СсылкаНаДокумент));
	//
	ДанныеОВладельце = ПолучитьДанныеОВладельце();
	ДанныеОКонтрагенте = ПолучитьДанныеОКонтрагенте(СсылкаНаДокумент.Контрагент);
	//
	Макет = ПолучитьОбщийМакет("АктВыполненныхРабот");	
	//
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	ОбластьПлатёжнаяИнформация = Макет.ПолучитьОбласть("ПлатёжнаяИнформация");
	ОбластьТекстСоглашения = Макет.ПолучитьОбласть("ТекстСоглашения");
	//
	ОбластьШапка.Параметры.Номер = СсылкаНаДокумент.Номер; 
	ОбластьШапка.Параметры.Дата = Формат(СсылкаНаДокумент.Дата,"ДЛФ=DD");
	//
	НомерТелефона = ДанныеОВладельце.НомерТелефона;
	ЭлектроннаяПочта = ДанныеОВладельце.ЭлектроннаяПочта;
	Фамилия = ДанныеОВладельце.Фамилия;
	Имя = ДанныеОВладельце.Имя;
	Отчество = ДанныеОВладельце.Отчество;
	ПлатёжнаяИнформация = ПолучитьПлатежнуюИнформацию();
	//
	ОбластьШапка.Параметры.Исполнитель = Строка(Фамилия)+" "+Строка(Имя)+" "+Строка(Отчество)+" , "+"тел.: "+Строка(НомерТелефона)+" , "+"E-mail: "+Строка(ЭлектроннаяПочта);
	//
	ОбластьШапка.Параметры.Заказчик = ДанныеОКонтрагенте.Наименование+" , "+"тел.: "+ДанныеОКонтрагенте.НомерТелефона+" , "+"E-mail: "+ДанныеОКонтрагенте.ЭлектроннаяПочта;
	ТабДок.Вывести(ОбластьШапка);
	//
	ТабДок.Вывести(ОбластьШапкаТаблицы);
	//
	ОбъектДок = СсылкаНаДокумент.ПолучитьОбъект();	
	Сумма = 0;
	Для Каждого Стр Из ОбъектДок.Услуги Цикл
		ОбластьСтрокаТаблицы.Параметры.НомерСтроки = Стр.НомерСтроки;
		ОбластьСтрокаТаблицы.Параметры.НаименованиеРаботУслуг = Стр.Услуга;
		ОбластьСтрокаТаблицы.Параметры.Количество = Стр.Количество;
		ОбластьСтрокаТаблицы.Параметры.Цена = Стр.Стоимость;
		ОбластьСтрокаТаблицы.Параметры.Сумма = Стр.Сумма;
		ОбластьСтрокаТаблицы.Параметры.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
		Сумма = Сумма+Стр.Сумма;
		ТабДок.Вывести(ОбластьСтрокаТаблицы);
	КонецЦикла;
	//
	ОбластьПодвал.Параметры.СуммаИтого = Сумма;
	ОбластьПодвал.Параметры.КоличествоУслуг = ОбъектДок.Услуги.Количество();
	ОбластьПодвал.Параметры.СуммаДокумента = Сумма;
	ОбластьПодвал.Параметры.ПерсональнаяСкидка = 0;
	ФормСтрока = "Л = ru_RU; ДП = Истина";
	ПарПредмета="рубль,рубля,рублей,м,копейка,копейки,копеек,ж,2";
	ПрописьЧисла = ЧислоПрописью(Сумма, ФормСтрока, ПарПредмета);
	ОбластьПодвал.Параметры.СуммаДокументаСловами = ПрописьЧисла; 
	ТабДок.Вывести(ОбластьПодвал);
	//
	ПолнаяПлатежнаяИнформация = "";
	Для Каждого Стр Из ПлатёжнаяИнформация Цикл
		ПолнаяПлатежнаяИнформация = ПолнаяПлатежнаяИнформация +
		Стр.Ключ + " : " + Стр.Значение
		+Символы.ПС
		;
	КонецЦикла;
	ОбластьПлатёжнаяИнформация.Параметры.ПлатёжнаяИнформация = ПолнаяПлатежнаяИнформация; 
	ТабДок.Вывести(ОбластьПлатёжнаяИнформация);
	//
	ТабДок.Вывести(ОбластьТекстСоглашения);
	//
	Акт.Вставить("ТабДок",ТабДок);
	//
	Возврат Акт;
КонецФункции