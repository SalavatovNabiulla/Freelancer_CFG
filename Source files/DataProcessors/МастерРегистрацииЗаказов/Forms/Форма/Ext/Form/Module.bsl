&НаКлиенте
Перем ЭтапРегистрации;

&НаСервере
Функция ПолучитьДанныеУслуги(Услуга)
	Данные = Новый Структура;
	Данные.Вставить("ЕдиницаИзмерения",Справочники.ЕдиницыИзмеренияУслуг.ПустаяСсылка());
	Данные.Вставить("Стоимость",0);
	//
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЦеныУслугСрезПоследних.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	ЦеныУслугСрезПоследних.Цена КАК Цена
	               |ИЗ
	               |	РегистрСведений.ЦеныУслуг.СрезПоследних КАК ЦеныУслугСрезПоследних
	               |ГДЕ
	               |	ЦеныУслугСрезПоследних.Услуга = &Услуга";
	Запрос.УстановитьПараметр("Услуга",Услуга);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаЗапроса.Следующий() Цикл
		Данные.ЕдиницаИзмерения = ВыборкаЗапроса.ЕдиницаИзмерения;
		Данные.Стоимость = ВыборкаЗапроса.Цена;
	КонецЦикла;
	//
	Возврат Данные;
КонецФункции

&НаКлиенте
Процедура ВыполнитьРасчёт(Элемент)
	ТД = ЭтаФорма.Элементы.Услуги.ТекущиеДанные;
	//
	Если Элемент.Имя = "УслугиКоличество" Или Элемент.Имя = "УслугиСтоимость" Тогда
		ТД.Сумма = ТД.Количество*ТД.Стоимость;
	ИначеЕсли Элемент.Имя = "УслугиСумма" Тогда
		ТД.Стоимость = ТД.Сумма/ТД.Количество;	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция СоздатьЗаказ()
	Док = Документы.Заказ.СоздатьДокумент();
	Док.Дата = ТекущаяДата();
	Док.Контрагент = Контрагент;
	Док.ВалютаДокумента = Валюта;
	Док.СтатусЗаказа = Перечисления.СтатусыЗаказа.Планируется;	
	Для Каждого Стр Из ЭтотОбъект.Услуги Цикл
		ТЧ = Док.Услуги.Добавить();
		ТЧ.Услуга = Стр.Услуга;
		ТЧ.Количество = Стр.Количество;
		ТЧ.Статус = Перечисления.СтатусыУслуг.Принят;
		ТЧ.ЕдиницаИзмерения = Стр.ЕдиницаИзмерения;
		ТЧ.Стоимость = Стр.Стоимость;
		ТЧ.Сумма = Стр.Сумма;
	КонецЦикла;
	Док.Записать(РежимЗаписиДокумента.Проведение);
	Возврат Док.Ссылка;
КонецФункции

&НаКлиенте
Процедура Далее(Команда)
	Если ЭтапРегистрации = 0 Тогда
		ЭтаФорма.Элементы.ГруппаНачальныйЭкран.Видимость = Ложь;
		ЭтаФорма.Элементы.ГруппаКонтрагент.Видимость = Истина;
	ИначеЕсли ЭтапРегистрации = 1 Тогда
		ТД = ЭтаФорма.Элементы.Контрагенты.ТекущиеДанные;
		Контрагент = ТД.Ссылка;
		//
		ЭтаФорма.Элементы.ГруппаКонтрагент.Видимость = Ложь;
		ЭтаФорма.Элементы.ГруппаФинансоваяИнформация.Видимость = Истина;
	ИначеЕсли ЭтапРегистрации = 2 Тогда
		ТД = ЭтаФорма.Элементы.Валюты.ТекущиеДанные;
		Валюта = ТД.Ссылка;
		//
		ЭтаФорма.Элементы.ГруппаФинансоваяИнформация.Видимость = Ложь;
		ЭтаФорма.Элементы.ГруппаУслуги.Видимость = Истина;
	ИначеЕсли ЭтапРегистрации = 3 Тогда
		Заказ = СоздатьЗаказ();
		//
		ЭтаФорма.Элементы.ГруппаУслуги.Видимость = Ложь;
		ЭтаФорма.Элементы.ГруппаЗавершение.Видимость = Истина;
		ЭтаФорма.Элементы.Далее.Заголовок = "Закрыть";
	ИначеЕсли ЭтапРегистрации = 4 Тогда
		Оповестить("ЗаказЗарегистрирован",Истина,ЭтаФорма);
		ЭтаФорма.Закрыть();
	КонецЕсли;
	//
	ЭтапРегистрации = ЭтапРегистрации+1;
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЭтапРегистрации = 0;
КонецПроцедуры

&НаКлиенте
Процедура УслугиУслугаПриИзменении(Элемент)
	ТД = ЭтаФорма.Элементы.Услуги.ТекущиеДанные;
	ДанныеУслуги = ПолучитьДанныеУслуги(ТД.Услуга);
	ТД.ЕдиницаИзмерения = ДанныеУслуги.ЕдиницаИзмерения;
	ТД.Стоимость = ДанныеУслуги.Стоимость;
	//
	ТД.Сумма = ТД.Количество*ТД.Стоимость;
КонецПроцедуры

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	ВыполнитьРасчёт(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСтоимостьПриИзменении(Элемент)
	ВыполнитьРасчёт(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)
	ВыполнитьРасчёт(Элемент);
КонецПроцедуры
